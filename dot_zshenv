# .zshenv --- Setup 'environment'
# Mike Barker <mike@thebarkers.com>
# Created: November 3rd, 2022
# Updated: October 5th, 2025

## Description:
# The first personal startup file to be sourced
# Source in all shells, unless option -f is set
# Source after /etc/zshenv
# Source before /etc/zprofile, then /etc/profile, then ~/.zprofile.
# This file should contain commands to set the command search path, plus other
# important environment variables. `.zshenv' should not contain commands that
# produce output or assume the shell is attached to a tty.

## Note:
# On macOS, the /etc/zprofile and path_helper command will clobber PATH order 
# https://gist.github.com/Linerre/f11ad4a6a934dcf01ee8415c9457e7b2#solutions
# I have decided that I would rather NOT have the system paths hidden by my
# local paths... this seems to be a better from a security standpoint, we will
# see how this works out.

# print when this file is sourced
[[ -n "$DEBUG" ]] && echo "~/.zshenv"

## Configure the homebrew environment
if [[ -x /opt/homebrew/bin/brew ]]; then
    BREW="/opt/homebrew/bin/brew"
elif [[ -x /usr/local/bin/brew ]]; then
    BREW="/usr/local/bin/brew"
elif [[ -x /home/linuxbrew/.linuxbrew/bin/brew ]]; then
    BREW="/home/linuxbrew/.linuxbrew/bin/brew"
elif [[ -x "$HOME/.linuxbrew/bin/brew" ]]; then
    BREW="$HOME/.linuxbrew/bin/brew"
else
    BREW=""
fi
if [[ "$BREW" != "" ]]; then
    eval "$($BREW shellenv)"
fi
unset BREW

## Configure pyenv environment
if [[ -d ${HOME}/.pyenv ]]; then
    export PYENV_ROOT="$HOME/.pyenv"
    export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init --path)"
fi

## Configure rbenv environment
if [[ $(command -v rbenv) ]]; then
    eval "$(rbenv init -)"
fi

## Fixup vscode-server path
# If there is a vscode-server path element, it must be before any path that has
# the localy installed code server. This code will find the first path element
# that has vscode-server, remove it from the path array and prepend it to the
# start.

# Find the index of any element in the $path array that contains .vscode-server
# If found (i.e. the index is valid), move the .vscode-server path to the front
vscode_index=${path[(i)*.vscode-server*]}
if [[ $vscode_index -le ${#path} ]]; then
  vscode_path=${path[vscode_index]}
  path[$vscode_index]=()
  path=($vscode_path $path)
fi


## Configure the path
# Feel free to add app or system specific paths here since any path that does
# not exist on the current system will not be added to the system path.

# Create an newpath array
newpath=(
    # Prepend paths
    # Add the existing paths to the newpath array
    $path
    # Append paths
    /usr/local/sbin
    ~/bin
    ~/.local/bin
)

# Replace the system path using the newpath array,
# only existing paths will be added to the new path.
path=($^newpath(N))
unset newpath

# Remove duplicate entries from the system path
typeset -U path
