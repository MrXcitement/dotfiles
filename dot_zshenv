#!/bin/zsh

# .zshenv --- Usually run for every zsh
# Mike Barker <mike@thebarkers.com>
# November 3rd, 2022

# If on macos, use path_helper to seed the path
PATH_HELPER=/usr/libexec/path_helper
if [[ -x  $PATH_HELPER ]]; then
    unset PATH
    eval $($PATH_HELPER -s)
fi
unset PATH_HELPER

# Configure the homebrew environment
if (( ! $+commands[brew] )); then
    if [[ -x /opt/homebrew/bin/brew ]]; then
        BREW="/opt/homebrew/bin/brew"
    elif [[ -x /usr/local/bin/brew ]]; then
        BREW="/usr/local/bin/brew"
    elif [[ -x /home/linuxbrew/.linuxbrew/bin/brew ]]; then
        BREW="/home/linuxbrew/.linuxbrew/bin/brew"
    elif [[ -x "$HOME/.linuxbrew/bin/brew" ]]; then
        BREW="$HOME/.linuxbrew/bin/brew"
    else
        BREW=""
    fi
    if [[ "$BREW" != "" ]]; then
        eval "$($BREW shellenv)"
    fi
    unset BREW
fi

# Configure pyenv environment
if [[ -d ${HOME}/.pyenv ]]; then
    export PYENV_ROOT="$HOME/.pyenv"
    export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init --path)"
fi

# Configure rbenv environment
if [[ $(command -v rbenv) ]]; then
    eval "$(rbenv init -)"
fi

## Configure the path
# Feel free to add system specific paths here
# since any path that does not exist on the
# current system will not be added to the
# system path.

# Add before existing paths
newpath=(
~/bin
~/.local/bin
~/.dotnet/tools
/opt/microsoft/bin
# Add existing paths
$path
# Add after existing paths
/usr/local/sbin)

# Replace the system path using the newpath array,
# only existing paths will be added to the new path.
path=($^newpath(N))
unset newpath

# Remove duplicate entries from the system path
typeset -U path
