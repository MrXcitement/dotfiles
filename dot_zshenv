# .zshenv --- Usually run for every zsh
# Mike Barker <mike@thebarkers.com>
# November 3rd, 2022

## On macOS handle path issues introduced by system config
# See: https://gist.github.com/Linerre/f11ad4a6a934dcf01ee8415c9457e7b2#solutions
if [[ $OSTYPE =~ "darwin" ]]; then
    unsetopt GLOBAL_RCS
    eval `/usr/libexec/path_helper -s`
    # Check if /etc/zprofile has been changed
    if [[ "$(crc32 '/etc/zprofile')" != "703cd3c8" ]]; then
        echo "$(tput setaf 3)WARNING!$(tput sgr 0)"\
             "File \`/etc/zprofile\` seems to has been changed since checkpoint."
    fi
    # Check for system startup files that did not exist when
    # macOS Sequioa was installed.
    for file in '/etc/zshenv' '/etc/zlogin' '/etc/zlogout'; do
        if [[ -f "$file" ]]; then
            echo "$(tput setaf 3)WARNING!$(tput sgr 0)"\
                 "File \`$file\` exists and should not."
        fi
    done
fi

## Configure the homebrew environment
if [[ -x /opt/homebrew/bin/brew ]]; then
    BREW="/opt/homebrew/bin/brew"
elif [[ -x /usr/local/bin/brew ]]; then
    BREW="/usr/local/bin/brew"
elif [[ -x /home/linuxbrew/.linuxbrew/bin/brew ]]; then
    BREW="/home/linuxbrew/.linuxbrew/bin/brew"
elif [[ -x "$HOME/.linuxbrew/bin/brew" ]]; then
    BREW="$HOME/.linuxbrew/bin/brew"
else
    BREW=""
fi
if [[ "$BREW" != "" ]]; then
    eval "$($BREW shellenv)"
fi
unset BREW

## Configure pyenv environment
if [[ -d ${HOME}/.pyenv ]]; then
    export PYENV_ROOT="$HOME/.pyenv"
    export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init --path)"
fi

## Configure rbenv environment
if [[ $(command -v rbenv) ]]; then
    eval "$(rbenv init -)"
fi

## Fix the vscode-server path
# If there is a vscode-server path element, it must be before any path that has
# the localy installed code server. This code will find the first path element
# that has vscode-server, remove it from the path array and prepend it to the
# start.

# Find the index of any element in the $path array that contains ".vscode-server"
# If found (i.e., the index is valid), move the .vscode-server path to the front
vscode_index=${path[(i)*.vscode-server*]}
if [[ $vscode_index -le ${#path} ]]; then
  vscode_path=${path[vscode_index]}
  path[$vscode_index]=()
  path=($vscode_path $path)
fi


## Configure the path
# Feel free to add app specific paths here since any path that does not exist
# on the current system will not be added to the system path.

# Create an newpath array
newpath=(
    # Prepend paths
    ~/bin
    ~/.local/bin
    # Add the existing paths to the newpath array
    $path
    # Append paths
    /usr/local/sbin)

# Replace the system path using the newpath array,
# only existing paths will be added to the new path.
path=($^newpath(N))
unset newpath

# Remove duplicate entries from the system path
typeset -U path
